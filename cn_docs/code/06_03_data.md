---
layout: default
title: 双重差分估计器的数据
parent: Stata代码
nav_order: 3
mathjax: true
image: "../../../assets/images/DiD.png"
---

# 包的數據生成
{: .no_toc }

## 目录
{: .no_toc .text-delta }

1. TOC
{:toc}

---

## 生成样本数据

在这里，我们生成一个具有異質處理的測試數據集，我們將用於示例代碼：

```stata
clear

local units = 30
local start = 1
local end 	= 60

local time = `end' - `start' + 1
local obsv = `units' * `time'
set obs `obsv'

egen id	   = seq(), b(`time')  
egen t 	   = seq(), f(`start') t(`end') 	

sort  id t
xtset id t


set seed 20211222

gen Y 	    		= 0		// 結果變量	
gen D 	    		= 0		// 干預變量
gen cohort      = .  	// 處理隊列
gen effect      = .		// 處理效應大小
gen first_treat = .		// 每個隊列的處理發生時間
gen rel_time	= .     // 時間 - first_treat

levelsof id, local(lvls)
foreach x of local lvls {
	local chrt = runiformint(0,5)	
	replace cohort = `chrt' if id==`x'
}


levelsof cohort , local(lvls) 
foreach x of local lvls {
	
	local eff = runiformint(2,10)
		replace effect = `eff' if cohort==`x'
			
	local timing = runiformint(`start',`end' + 20)	// 
	replace first_treat = `timing' if cohort==`x'
	replace first_treat = . if first_treat > `end'
		replace D = 1 if cohort==`x' & t>= `timing' 
}

replace rel_time = t - first_treat
replace Y = id + t + cond(D==1, effect * rel_time, 0) + rnormal()



// 生成前導和滯後（在一些命令中使用）

summ rel_time
local relmin = abs(r(min))
local relmax = abs(r(max))

	// 前導
	cap drop F_*
	forval x = 2/`relmin' {  // 刪除第一個前導
		gen F_`x' = rel_time == -`x'
	}

	
	// 滯後
	cap drop L_*
	forval x = 0/`relmax' {
		gen L_`x' = rel_time ==  `x'
	}
	
	
// 生成control_cohort變量（在一些命令中使用）

gen never_treat = first_treat==.

sum first_treat
gen last_cohort = first_treat==r(max) // 最新或從未處理隊列的虛擬變量


// 生成gvar變量（在一些命令中使用）
gen gvar = first_treat
recode gvar (. = 0)

```	
	
生成圖表：

```stata
xtline Y, overlay legend(off)
```

<img src="../../../assets/images/test_data.png" height="300">